---
title: "Aritmética básica"
number-sections: true
lang: pt-BR
---


## Operações aritméticas 

:::{#task}
As [operações](https://docs.gap-system.org/doc/ref/chap4_mj.html#X7B66C8707B5DE10A) mais comuns em GAP são `+` (adição), `-` (subtração), `*` (multiplicação), `/` (divisão), `mod`, (resto) `^` (exponenciação). Experimente com os seguintes exemplos.
```matlab
gap> 2+3;
5
gap> 2/4;
1/2
gap> (2+3)*(1/2);
5/2
gap> 2^3*3;
24
gap> 2^-2*3;
3/4
gap> 2.0;
2.
gap> 2.01+3.11;
5.12
gap> 1/3;
1/3
gap> 1.0/3;
0.333333
```
:::

Note a diferença entre trabalhar com valores exatos (como por exemplo `1/3`) e valores aproximados (por exemplo `1.0/3`).

## Operações lógicas (booleanas)

Para comparar valores, usamos a operação `=` cujo valor é um [valor lógico](https://docs.gap-system.org/doc/ref/chap20_mj.html#X787B4AB77A2F5E14) ou seja `true` ou `false`. As operações com valores lógicos são `not`, `and` e `or`. 
```matlab
gap> 2 + 3 = 5;
true
gap> 2 + 3 = 5 or 2 + 3 = 6;
true
gap> 2 + 3 = 5 and 2 + 3 = 6;
false
gap> 2 + 3 = 5 and  not 2 + 3 = 6;
true
```

## Corpos finitos

Quando trabalhamos com estruturas algébricas, precisamos trabalhar com corpos além do corpo $\mathbb Q$. Aqui em baixo veremos alguns exemplos para aritmética com corpos finitos. Sabemos que o [grupo multiplicativo de um corpo finito é cíclico](https://schcs.github.io/AlgebraNotesPt/GruposECorpos/geradores.html#grupo-c%C3%ADclico). Isso quer dizer que para todo corpo finito $\mathbb F$, existe um elemento $z\in \mathbb F$ tal que os elementos não nulos de $\mathbb F$ são as potências de $z$; ou seja, 
$$
\mathbb F=\{0\}\cup \{z^0,z^1,\ldots,z^{q-2}\}
$$
onde $q=|\mathbb F|$. Para uma potência de primo `q`,  função `Z( q )` devolve um gerador explícito do corpo finito `GF( q )` (Galois Field). Os  elementos não nulos de `GF( q )` são representados como potências de `Z( q )`. Considere os seguintes exemplos. 
```matlab
gap> F := GF(11);
GF(11)
gap> e := One( F );
Z(11)^0
gap> z := Zero( F );
0*Z(11)
gap> p := PrimitiveElement( F );
Z(11)
gap> e+p;
Z(11)^8
gap> e+p^2;
Z(11)^4
gap> IntFFE( e+p^2 );
5
```


## Variáveis

As variáveis guardam valores. Uma variável é um string de letras, números e alguns carateres especiais. Aqui é um exemplo simples.
```matlab
gap> a := 2;
2
gap> a3 := 3;
3
gap> a_5 := 5;
5
gap> a := 2; 
2
gap> b := 3;
3
gap> a + b; a*b;
5
6

```
Variávies podem conter valores mais complexos, como nos seguintes exemplos.

```matlab
gap> s1 := "Hello";
"Hello"
gap> s2 := " World!";
" World!"
gap> Concatenation( s1, s2 );
"Hello World!"
```

## Funções

Funções em ```GAP``` podem ser usadas para realizar computações que queremos fazer várias vezes com diferentes objetos. Por exemplo a função que, dado ```x``` computa ```3*x+1``` pode ser definida de duas maneiras diferentes.
```matlab
gap> f1 := function( x )
> return 3*x+1;
> end;
function( x ) ... end
gap> f1( 5 );
16
```
```matlab
gap> f2 := x -> 3*x + 1;
function( x ) ... end
gap> f2( -2 );
-5
gap> f1 = f2;
false
# As funções f1 e f2 fazem a mesma coisa, mas são objetos distintos
```

Nos exemplos anteriores, a função não precisa de variáveis adicionais para executar sua tarefa. Isso pode acontecer com funções simples, mas uma função mais complicada pode precisar introduzir as suas próprias variáveis, chamadas de *variáveis locais*. Considere a seguinte implementação do exemplo em cima.
```matlab
gap> f3 := function( x )
> local y;
> y := 3*x;
> return y+1;
> end;
function( x ) ... end
gap> f3(4);
13
gap> y;
Error, Variable: 'y' must have a value
not in any function at *stdin*:44
```
As últimas linhas do exemplo anterior mostram que o variável ```y``` não está visível fora da função. Isso permite que variáveis com o mesmo nome sejam usadas em várias funções sem que sejam confundidas. O mesmo também impede que a varíavel local ```y``` definida dentro da função ```f3``` seja confundida com uma possível variável global também chamada de ```y```. O seguinte código mostra que o nome `y` tem significado diferente dentro e fora da função. 
```matlab
gap> f3 := function( x )
> local y;
> y := 3*x;
> Print( "O valor de y é ", y, "\n" );
> return y+1;
> end;
function( x ) ... end
gap> y := -5;
-5
gap> f3(10);
O valor de y é 30
31
gap> y;
-5
```



